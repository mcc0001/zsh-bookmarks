#!/usr/bin/env zsh

local bookmark_file="$(<"$ZPM_BOOKMARKS_FILE")"
local bookmark_path
local bookmark_name
local output_sep=" ${c[yellow]}${c[bold]}--${c[reset]} "

if [[ -n "$1" ]]; then
  @bookmark_path_colorize "$(grep -E "\|${1}$" <<< "$bookmark_file" | awk -F'|' '{print $1}')"
  echo
else
  # First pass: compute max name width
  local max_name_width=0
  while IFS= read -r line; do
    bookmark_name="${line#*|}"
    [[ -n "$bookmark_name" ]] && (( ${#bookmark_name} > max_name_width )) && max_name_width=${#bookmark_name}
  done <<< "$bookmark_file"

  # Second pass: print aligned output
  while IFS= read -r line; do
    bookmark_path="${line%|*}"
    bookmark_name="${line#*|}"

    if [[ -n "$bookmark_path" && -n "$bookmark_name" ]]; then
      local colored_name="$(@bookmark_name_colorize $bookmark_name)"
      local colored_path="$(@bookmark_path_colorize $bookmark_path)"
      local padding_width=$(( max_name_width - ${#bookmark_name} ))
      printf "%s%${padding_width}s%s%s\n" "$colored_name" "" "$output_sep" "$colored_path"
    fi
  done <<< "$bookmark_file"
fi
